import org.cloudfoundry.credhub.gradlebuild.DependenciesGraphPlugin

buildscript {
    ext {
        apacheCommonsLang3Version = '3.14.0'
        apacheCommonsIoVersion = '2.15.1'
        apacheHttpClientVersion = '4.5.14'
        asciiDoctorConvertPluginVersion = '2.4.0'
        bcpkixFipsVersion = '1.0.7'
        bcFipsVersion = '1.0.2.4'
        commonsCodecVersion = '1.16.0' // remove this after deleting (now deprecated) spring-security-oauth2
        flywayVersion = '7.15.0'
        guavaVersion = '33.0.0-jre'
        h2Version = '1.4.200'
        jsonPathVersion = '2.8.0'
        kotlinVersion = '1.9.22'
        ktlintVersion = '0.43.2'
        passayVersion = '1.6.4'
        postgresqlJdbcVersion = '42.7.1'
        spotBugsToolVersion = '4.7.3'
        springRestDocsVersion = '2.0.8.RELEASE'
        springBootVersion = '2.7.18'
        springSecurityOauth2Version = '2.5.2.RELEASE'
        springSecurityOauth2AutoconfigureVersion = '2.6.8'
        mariadbJdbcVersion = '2.7.11' // Bumping to v3 breaks some pipeline jobs, so pinning to v2 for now. v2 (current version) is stable and will be supported until about September 2025 (https://mariadb.com/kb/en/about-mariadb-connector-j/).
        snakeyamlVersion = '2.2'
    }
    repositories {
        mavenCentral()
        maven { url("https://repo.spring.io/plugins-release") }
        maven { url("https://plugins.gradle.org/m2/") }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.owasp:dependency-check-gradle:9.0.7")
        classpath("org.bouncycastle:bc-fips:${bcFipsVersion}")
        classpath("org.bouncycastle:bcpkix-fips:${bcpkixFipsVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.50.0'
    id 'com.github.spotbugs' version '6.0.4'
}

apply plugin: DependenciesGraphPlugin
apply plugin: "org.owasp.dependencycheck"

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

subprojects {
    plugins.withType(JavaPlugin) {
        dependencies {
            implementation("org.yaml:snakeyaml:${snakeyamlVersion}")
            implementation("com.h2database:h2:${h2Version}")
            implementation("com.google.guava:guava:${guavaVersion}")
            testImplementation("org.mockito:mockito-inline")
        }
    }
}

// Override spring boot's kotllin version dependency
ext['kotlin.version'] = '${kotlinVersion}'

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)