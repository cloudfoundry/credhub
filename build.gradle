import org.cloudfoundry.credhub.gradlebuild.DependenciesGraphPlugin

buildscript {
    ext {
        apacheCommonsLang3Version = '3.10'
        apacheCommonsIoVersion = '1.3.2'
        apacheHttpClientVersion = '4.5.12'
        asciiDoctorPluginVersion = '1.6.1'
        bcpkixFipsVersion = '1.0.2'
        bcFipsVersion = '1.0.1'
        commonsCodecVersion = '1.14' // remove this after deleting (now deprecated) spring-security-oauth2
        flywayVersion = '5.2.4' // pinning to v5 because upgrading to v6+ directly breaks the upgrade of (v4->v5->v6) unless we can work around [this breaking change](https://stackoverflow.com/questions/49063385/flyway-5-0-7-warning-about-using-schema-version-table).
        guavaVersion = '29.0-jre'
        h2Version = '1.4.199'
        jsonPathVersion = '2.4.0'
        kotlinVersion = '1.3.72'
        ktlintVersion = '0.37.2'
        mariadbJdbcVersion = '2.3.0' // When using a MariaDB with a driver of version 2.4.0 or greater, FlyWay 5 does not know how to handle the driver reporting itself as "MariaDB" and will throw an exception causing CredHub to be unable to start. Do not upgrade until this issue is resolved.
        passayVersion = '1.6.1'
        postgresqlJdbcVersion = '42.3.2'
        spotBugsToolVersion = '3.1.9'
        spotBugsGradlePluginVersion = '1.6.9'
        springBootVersion = '2.2.7.RELEASE'
        springRestDocsVersion = '2.0.4.RELEASE'
        springSecurityOauth2Version = '2.5.0.RELEASE'
        springSecurityOauth2AutoconfigureVersion = '2.3.1.RELEASE'
        log4jVersion = '2.17.1' // this pinning can be removed when we bump to a spring boot starter version that has 2.17.1+ (due to CVE https://github.com/advisories/GHSA-jfh8-c2jp-5v3q in log4j <= 2.15.0)
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url("https://repo.spring.io/plugins-release") }
        maven { url("https://plugins.gradle.org/m2/") }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.owasp:dependency-check-gradle:5.3.2.1")
        classpath("org.bouncycastle:bc-fips:${bcFipsVersion}")
        classpath("org.bouncycastle:bcpkix-fips:${bcpkixFipsVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
}

apply plugin: DependenciesGraphPlugin
apply plugin: "org.owasp.dependencycheck"

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

subprojects {
    plugins.withType(JavaPlugin) {
        dependencies {
            testImplementation("org.mockito:mockito-inline:3.3.3")
        }
    }
}
