buildscript {
    ext {
        grpcVersion = '1.54.0'
    }
    repositories {
        mavenCentral()
        maven { url("https://repo.spring.io/plugins-release") }
        maven { url("https://plugins.gradle.org/m2/") }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciiDoctorPluginVersion}")
        classpath("com.google.protobuf:protobuf-gradle-plugin:0.9.2")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'
apply from: "${rootDir.toPath()}/spring-rest-docs-config.gradle"
apply from: "${rootDir.toPath()}/kotlin.gradle"
apply from: "${rootDir.toPath()}/ktlint.gradle"
apply from: "${rootDir.toPath()}/checkstyle-config.gradle"
apply from: "${rootDir.toPath()}/pmd-config.gradle"
apply from: "${rootDir.toPath()}/spotbugs.gradle"
apply plugin: "com.google.protobuf"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation project(":backends:shared-backend-configuration")
    implementation project(":backends:remote")
    implementation project(":components:audit")
    implementation project(":components:auth")
    implementation project(":components:constants")
    implementation project(":components:credentials")
    implementation project(":components:encryption")
    implementation project(":components:errors")
    implementation project(":components:generate")
    implementation project(":components:management")
    implementation project(":components:permissions")
    implementation project(":components:string-utilities")
    implementation project(":components:time-support")

    testImplementation project(path: ":components:test-support", configuration: "testOutput")

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation('org.springframework.boot:spring-boot-starter-validation')
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("junit:junit")

    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    // DB
    implementation("org.postgresql:postgresql:${postgresqlJdbcVersion}")

    implementation("org.flywaydb:flyway-core:${flywayVersion}")
    implementation("org.mariadb.jdbc:mariadb-java-client:${mariadbJdbcVersion}")
    implementation("com.h2database:h2:${h2Version}")

    // Other
    api("org.bouncycastle:bc-fips:${bcFipsVersion}")
    api("org.bouncycastle:bcpkix-fips:${bcpkixFipsVersion}")

    // proto
    implementation "com.google.api.grpc:proto-google-common-protos:2.15.0"
    implementation "io.grpc:grpc-services:${grpcVersion}"

    implementation("com.google.guava:guava:${guavaVersion}")
    implementation("org.apache.commons:commons-lang3:${apacheCommonsLang3Version}")
    implementation("commons-codec:commons-codec:${commonsCodecVersion}")
    implementation("com.jayway.jsonpath:json-path:${jsonPathVersion}")

    testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc:$springRestDocsVersion")
    asciidoctor("org.springframework.restdocs:spring-restdocs-asciidoctor:$springRestDocsVersion")
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
}

sourceSets {
    test {
        resources {
            srcDirs += [
                    project(":applications:credhub-api").sourceSets.main.resources,
                    project(":applications:credhub-api").sourceSets.test.resources
            ]
        }
    }
}

test {
    testLogging {
        events "started", "passed", "failed", "skipped"
        exceptionFormat "full"
    }

    afterTest { descriptor, result ->
        def totalTime = result.endTime - result.startTime
        println "Total time of '$descriptor.name' was $totalTime ms"
    }

    systemProperties = System.properties
    systemProperties["spring.profiles.active"] = System.getProperty("spring.profiles.active", "unit-test-h2")
    systemProperties["java.security.egd"] = System.getProperty("java.security.egd", "file:/dev/urandom")
    outputs.upToDateWhen { false }
}

task buildAndCopyRestDocsIntoSpringStaticAssetLocation(type: Copy) {
    dependsOn asciidoctor

    from file("build/asciidoc/html5/index.html")
    into project(":applications:credhub-api").file("src/main/resources/static/docs/")
}
asciidoctor.finalizedBy(buildAndCopyRestDocsIntoSpringStaticAssetLocation)

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.22.3"
    }
}
