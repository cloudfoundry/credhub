buildscript {
    repositories {
        def artifactRepoUrl = System.getenv("ARTIFACTORY_URL")
        if (artifactRepoUrl != null) {
            maven {
                url artifactRepoUrl
                credentials {
                    username = System.getenv("ARTIFACTORY_USER")
                    password = System.getenv("ARTIFACTORY_PASS")
                }
            }
        }
        else {
            mavenCentral()
            maven { url("https://repo.spring.io/plugins-release") }
            maven { url("https://plugins.gradle.org/m2/") }
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'io.spring.dependency-management'

apply from: "${rootDir.toPath()}/kotlin.gradle"
apply from: "${rootDir.toPath()}/ktlint.gradle"
apply from: "${rootDir.toPath()}/checkstyle-config.gradle"
apply from: "${rootDir.toPath()}/pmd-config.gradle"

dependencies {

    implementation project(":components:audit")
    implementation project(":components:credentials")
    implementation project(":components:errors")
    implementation project(":components:permissions")
    implementation project(":components:string-utilities")

    testImplementation project(path: ":components:test-support", configuration: "testOutput")

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation('org.springframework.boot:spring-boot-starter-validation')
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("junit:junit")

    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    implementation("com.jayway.jsonpath:json-path")
    implementation("com.google.guava:guava:${guavaVersion}")
    implementation("org.apache.commons:commons-lang3")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
}

sourceSets {
    test {
        resources {
            srcDirs += [
                    project(":applications:credhub-api").sourceSets.main.resources,
                    project(":applications:credhub-api").sourceSets.test.resources
            ]
        }
    }
}
