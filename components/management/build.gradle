buildscript {
    repositories {
        def artifactRepoUrl = System.getenv("ARTIFACTORY_URL")
        if (artifactRepoUrl != null) {
            maven {
                url artifactRepoUrl
                credentials {
                    username = System.getenv("ARTIFACTORY_USER")
                    password = System.getenv("ARTIFACTORY_PASS")
                }
            }
        }
        else {
            mavenCentral()
            maven { url("https://repo.spring.io/plugins-release") }
            maven { url("https://plugins.gradle.org/m2/") }
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'io.spring.dependency-management'

apply from: "${rootDir.toPath()}/kotlin.gradle"
apply from: "${rootDir.toPath()}/ktlint.gradle"
apply from: "${rootDir.toPath()}/checkstyle-config.gradle"
apply from: "${rootDir.toPath()}/pmd-config.gradle"

repositories {
    def artifactRepoUrl = System.getenv("ARTIFACTORY_URL")
    if (artifactRepoUrl != null) {
        maven {
            url artifactRepoUrl
            credentials {
                username = System.getenv("ARTIFACTORY_USER")
                password = System.getenv("ARTIFACTORY_PASS")
            }
        }
    }
    else {
        mavenCentral()
    }
}

dependencies {
    implementation project(":components:errors")

    implementation("org.springframework.boot:spring-boot-starter-web")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("junit:junit")
    testImplementation project(path: ":components:test-support", configuration: "testOutput")
    testImplementation("org.flywaydb:flyway-core:${flywayVersion}")
    testImplementation("org.flywaydb:flyway-database-postgresql:${flywayVersion}")

    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    implementation("org.apache.commons:commons-lang3")
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
}

sourceSets {
    test {
        resources {
            srcDirs += [
                    project(":applications:credhub-api").sourceSets.main.resources,
                    project(":applications:credhub-api").sourceSets.test.resources
            ]
        }
    }
}
